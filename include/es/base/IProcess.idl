/*
 * Copyright (c) 2006, 2007
 * Nintendo Co., Ltd.
 *
 * Permission to use, copy, modify, distribute and sell this software
 * and its documentation for any purpose is hereby granted without fee,
 * provided that the above copyright notice appear in all copies and
 * that both that copyright notice and this permission notice appear
 * in supporting documentation.  Nintendo makes no
 * representations about the suitability of this software for any
 * purpose.  It is provided "as is" without express or implied warranty.
 */

#ifndef NINTENDO_ES_BASE_IPROCESS_IDL_INCLUDED
#define NINTENDO_ES_BASE_IPROCESS_IDL_INCLUDED

#include "es/base/ICallback.idl"
#include "es/base/IFile.idl"
#include "es/base/IPageable.idl"
#include "es/base/IThread.idl"
#include "es/base/IMonitor.idl"
#include "es/naming/IContext.idl"

module es
{
    /** This interface provides methods for handling child processes of the current process.
     */
    interface IProcess : IInterface
    {
        #pragma ID IProcess = "DCE:3b6bc7bd-44c2-11dc-9c02-0009bf000001";

        /**
         * Kills this process.
         */
        void kill();
        /**
         * Starts this process, which executes a program <code>file</code> with arguments.
         * @param file an executable file.
         * @param command arguments to be passed to the program.
         */
        void start(in IFile file, in string command);
        /**
         * Starts this process, which executes a program <code>file</code>.
         * @param file an executable file.
         */
        void start(in IFile file);
        /**
         * Starts this process.
         */
        void start();
        /**
         * Waits until this process is complete.
         */
        long wait();
        /**
         * Tests whether this process has exited.
         * @return true if this process has exited. Otherwise, false.
         */
        boolean hasExited();
        /**
         * The root of the namespace for this process.
         */
        attribute IContext root;
        /**
         * The standard input for this process.
         */
        attribute IStream input;
        /**
         * The standard output for this process.
         */
        attribute IStream output;
        /**
         * The standard error for this process.
         */
        attribute IStream error;
        /**
         * The current working context in the namespace for this process.
         */
        attribute IContext current;

        /**
         * The exit value of this process.
         */
        readonly attribute long exitValue;
    };

    /** This interface provides methods for handling the current process.
     */
    interface ICurrentProcess : IInterface
    {
        #pragma ID ICurrentProcess = "DCE:3c10827b-44c2-11dc-9c02-0009bf000001";
        const unsigned long PROT_NONE = 0x0;
        const unsigned long PROT_READ = 0x1;
        const unsigned long PROT_WRITE = 0x2;
        const unsigned long PROT_EXEC = 0x4;
        const unsigned long MAP_SHARED = 0x01;
        const unsigned long MAP_PRIVATE = 0x02;
        const unsigned long MAP_FIXED = 0x10;

        /** Terminates the currently running process.
         */
        void exit(in long status);

        /**
         * Maps the pageable object into memory.
         * @param start the preferable address to start mapping (usually 0).
         * @param length the mapping size.
         * @param prot <code>PROT_*</code> flags.
         * @param flags <code>MAP_*</code> flags.
         * @param pageable a pageable object to be mapped.
         * @param offset the start offset of <code>pageable</code>.
         * @return the address of the mapping.
         */
        any map(in any start, in long long length, in unsigned long prot, in unsigned long flags,
                in IPageable pageable, in long long offset);

        /**
         * Deletes the mappings.
         * @param start the address of the mapping.
         * @param length the size of the mapping.
         */
        void unmap(in any start, in long long length);

        /** Returns the currently executing thread.
         */
        ICurrentThread currentThread();

        /** Creates and returns a new thread.
         * @return          a new thread.
         * @param start     the function to be started in a new thread.
         * @param param     the argument to be passed to <code>start</code>.
         * @see IThread
         */
        IThread createThread(in any start, in any param);

        /** Causes the currently executing thread to temporarily pause
         * and allow other threads to execute.
         */
        void yield();

        /** Creates and returns an unlocked monitor.
         * @return          an unlocked monitor.
         * @see IMonitor
         */
        IMonitor createMonitor();

        /**
         * The root of the namespace.
         */
        readonly attribute IContext root;

        /**
         * The standard input.
         */
        readonly attribute IStream input;
        /**
         * The standard output.
         */
        readonly attribute IStream output;
        /**
         * The standard error.
         */
        readonly attribute IStream error;

        /**
         * Increments the program data space.
         * @param increment the increment size in bytes.
         */
        any setBreak(in long long increment);

        /**
         * Traces the system call invocation.
         * @param on    enable/disable trace.
         */
        boolean trace(in boolean on);

        /**
         * The current time.
         */
        readonly attribute long long now;

        /**
         * The current working context in the namespace.
         */
        attribute IContext current;
    };
};

#endif // NINTENDO_ES_BASE_IPROCESS_IDL_INCLUDED

